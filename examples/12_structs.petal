// A structure (struct) can be used to encapsulate multiple pieces of data in a single container. You can also define
// functions within that structure which use that data.

// NOTE: This function is not relevant to this example, it's just a forward declaration of `printf`, which is used later
//       on in the example.
extern func printf(format: &u8, ...args);

// Here, we define a type called `Person` which is a struct. Each struct can have associated fields and functions.
type Person = struct {
    name: &u8;
    age: i32;

    // A `static` function on a struct does not require an instance of the struct to be called. A static function is 
    // typically used for "constructor" or "factory" functions.
    static func create(name: &u8) -> Person {
        // A structure can be initialized using the following syntax:
        return {
            name = name,
            age = 0
        };
    }

    // A non-static function on a struct requires an instance of the struct to be called. This instance is a reference
    // to the struct value and can be accessed through the `this` keyword.
    func set_age(value: i32) {
        this.age = value;
    }

    func print() {
        printf("%s is %d years old", this.name, this.age);
    }
};

func main() {
    Person person = Person.create("Caoimhe");
    person.set_age(19);
    person.print();
}
